# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=pinentry
pkgver=1.0.0
pkgrel=1
pkgdesc='A collection of simple PIN or passphrase entry dialogs which utilize the Assuan protocol'
url='https://gnupg.org/software/pinentry/'
license=('GPL')
arch=('i686' 'x86_64')
makedepends=('ncurses-devel' 'libassuan-devel' 'libgpg-error-devel')
depends=('ncurses' 'libassuan' 'libgpg-error')
source=(https://gnupg.org/ftp/gcrypt/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig}
        pinentry-1.0.0-msysize.patch)
install=pinentry.install
sha256sums=('1672c2edc1feb036075b187c0773787b2afd0544f55025c645a71b4c2f79275a'
            'SKIP'
            '0d2cd0c6943a9a7128b4ce71806b513f8094521f6a7121195c4cc578d2b650a3')
validpgpkeys=('D8692123C4065DEA5E0F3AB5249B39D24F25E3B6') # Werner Koch

install=${pkgname}.install

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -p1 -i ${srcdir}/pinentry-1.0.0-msysize.patch

  ./autogen.sh
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  ./configure \
    --build=${CHOST} \
    --prefix=/usr \
    --enable-pinentry-curses \
    --enable-pinentry-tty \
    --disable-pinentry-gtk2 \
    --disable-pinentry-gnome3 \
    --disable-pinentry-qt \
    --enable-fallback-curses

  make
}

check() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make check
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
